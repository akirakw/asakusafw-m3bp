group 'com.example'

buildscript {
    repositories {
        if (System.getProperty("maven.local", "true") == "true") {
            mavenLocal()
        }
        maven { url 'http://asakusafw.s3.amazonaws.com/maven/releases' }
        maven { url 'http://asakusafw.s3.amazonaws.com/maven/snapshots' }
    }
    dependencies {
        classpath group: 'com.asakusafw.m3bp', 
                  name: 'asakusa-m3bp-gradle',
                  version: System.getProperty("asakusafw-m3bp.version")
    }
}

if (System.getProperty("maven.local", "true") == "true") {
    repositories {
        mavenLocal()
    }
}

apply plugin: 'asakusafw-sdk'
apply plugin: 'asakusafw-organizer'
apply plugin: 'asakusafw-m3bp'
apply plugin: 'eclipse'

asakusafw {
    sdk.incubating = System.getProperty('sdk.incubating', 'false') != 'false'
    sdk.testkit 'm3bp'
}

asakusafwOrganizer {
    hadoop {
        embed !System.getenv('HADOOP_CMD')
    }
    m3bp{
        useSystemHadoop System.getenv('HADOOP_CMD') as boolean
    }
    profiles.dev {
        assembly.into('.') {
            from 'src/main/conf'
            replace([
                'project.home.path' : file(System.getenv('PROJECT_HOME')).absolutePath,
                'project.home.uri' : uri(System.getenv('PROJECT_HOME')).toString(),
            ])
        }
    }
}

test {
    testLogging {
        showStandardStreams true
        showExceptions true
        showStackTraces true
        showCauses true
        exceptionFormat 'full'
    }
}
